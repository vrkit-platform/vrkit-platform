
set(RC_DATA_DIR "${CMAKE_BINARY_DIR}/resources")
file(MAKE_DIRECTORY ${RC_DATA_DIR})
#set(RC_DATA_SHADER_DIR ${RC_DATA_DIR}/shaders)
#file(MAKE_DIRECTORY ${RC_DATA_SHADER_DIR})

find_program(CMAKE_MC_COMPILER mc.exe
  PATHS "C:/Program Files (x86)/Windows Kits/10/bin/${WINDOWS_SDK_VERSION}/x64"
  DOC "WinSDK message compiler")
if(NOT CMAKE_MC_COMPILER)
    message(FATAL_ERROR "mc.exe was not found")
else()
    message(STATUS "mc.exe found at: ${CMAKE_MC_COMPILER}")
endif()


function(EMBED_MC_RESOURCE MC_FILE OUTPUT_HEADER_FILE OUTPUT_BIN_FILE)
  get_filename_component(MC_BASE_NAME ${MC_FILE} NAME_WLE)
  set(MC_OUTPUT_RC_FILE ${CMAKE_CURRENT_BINARY_DIR}/${MC_BASE_NAME}.rc)
  set(MC_OUTPUT_BIN_FILE ${CMAKE_CURRENT_BINARY_DIR}/${MC_BASE_NAME}_MSG00409.bin)
  set(MC_OUTPUT_HEADER_FILE ${CMAKE_CURRENT_BINARY_DIR}/${MC_BASE_NAME}.h)
  add_custom_command(
        OUTPUT ${MC_OUTPUT_RC_FILE} ${MC_OUTPUT_HEADER_FILE} ${MC_OUTPUT_BIN_FILE}
        COMMAND ${CMAKE_MC_COMPILER} -b -e h -h ${CMAKE_CURRENT_BINARY_DIR} -r ${CMAKE_CURRENT_BINARY_DIR} ${MC_FILE}
        DEPENDS ${MC_FILE}
    )

  set(${OUTPUT_BIN_FILE} ${MC_OUTPUT_BIN_FILE} PARENT_SCOPE)  
  set(${OUTPUT_HEADER_FILE} ${MC_OUTPUT_HEADER_FILE} PARENT_SCOPE)
  message(STATUS "MC OUTPUT HEADER FILE = ${MC_OUTPUT_HEADER_FILE}")

endfunction()

function(EMBED_RESOURCE RC_DATA_FILE RC_VAR_NAME OUTPUT_SRC_FILE_LIST)
  set(OUTPUT_SRC_FILE "${RC_DATA_DIR}/${RC_VAR_NAME}.c")
  add_custom_command(
    OUTPUT ${OUTPUT_SRC_FILE}
    DEPENDS ${RC_DATA_FILE}
    PRE_BUILD
    COMMAND python3
    ARGS ${CMAKE_SOURCE_DIR}/scripts/embed_resource.py -o ${OUTPUT_SRC_FILE} ${RC_VAR_NAME} ${RC_DATA_FILE}
    VERBATIM
  )
  list(APPEND ${OUTPUT_SRC_FILE_LIST} ${OUTPUT_SRC_FILE})
  set(${OUTPUT_SRC_FILE_LIST} ${${OUTPUT_SRC_FILE_LIST}} PARENT_SCOPE)
endfunction()

function(EMBED_SHADER_FX_RESOURCE FX_FILE RC_VAR_NAME OUTPUT_SRC_FILE_LIST)
  cmake_path(GET FX_FILE FILENAME FXO_FILE)
  set(FXO_FILE "${RC_DATA_DIR}/${FXO_FILE}o")
  message(NOTICE "Embedding shader ${FX_FILE} -> ${FXO_FILE}")
  add_custom_command(
    DEPENDS ${FX_FILE}
    OUTPUT ${FXO_FILE}
    PRE_BUILD
    COMMAND fxc
    ARGS /T fx_5_0 /Fo ${FXO_FILE} ${FX_FILE} 
    VERBATIM
  )

  set(LOCAL_OUTPUT_SRC_FILE_LIST)
  embed_resource("${FXO_FILE}" ${RC_VAR_NAME} LOCAL_OUTPUT_SRC_FILE_LIST)
  list(APPEND ${OUTPUT_SRC_FILE_LIST} ${LOCAL_OUTPUT_SRC_FILE_LIST})
  set(${OUTPUT_SRC_FILE_LIST} ${${OUTPUT_SRC_FILE_LIST}} PARENT_SCOPE)
endfunction()